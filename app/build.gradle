apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

/**
 * 获取当前时间
 * @return 当前时间
 */
def currentTime() {
    return new Date().format("yyyy_MM_dd HH:mm:ss", TimeZone.getTimeZone("GMT+8"))
}

def app_name_release = "Emoticon"
def debug_suffix = "debug"

def _compileSdkVersion = 24
def _buildToolsVersion = "24.0.3"
def _applicationId = "com.juhezi.emoticon"
def _minSdkVersion = 15
def _targetSdkVersion = 24
def _versionCode = 1
def _versionName = "1.0"


android {
    compileSdkVersion _compileSdkVersion
    buildToolsVersion _buildToolsVersion
    defaultConfig {
        applicationId _applicationId
        minSdkVersion _minSdkVersion
        targetSdkVersion _targetSdkVersion
        versionCode _versionCode
        versionName _versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "app_name", app_name_release)
        }
        debug {
            resValue("string", "app_name", "${app_name_release}_${debug_suffix}_${currentTime()}")
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile rootProject.ext.libSupportAppcomponent
    compile rootProject.ext.libSupportDesign
    compile rootProject.ext.libSupportRecyclerView
    compile rootProject.ext.libSupportCardView
    compile rootProject.ext.libSupportV4
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
repositories {
    mavenCentral()
}
